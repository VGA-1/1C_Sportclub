# Область СОБЫТИЯ_ФОРМЫ

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИнициализироватьПланировщик();
	
КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Дата = ТекущаяДата();
	
КонецПроцедуры

# КонецОбласти


# Область КОМАНДЫ

&НаКлиенте
Процедура Инициализировать(Команда)
	ИнициализироватьПланировщик();
КонецПроцедуры

# КонецОбласти


# Область ПЛАНИРОВЩИК

&НаКлиенте
Процедура ИнициализироватьПланировщик()  
	
	ПоказатьОповещениеПользователя("Выполняется инициализация планировщика.",,, БиблиотекаКартинок.Сегодня);
	
	ИнициализироватьПланировщикНаСервере();
	
	ПоказатьОповещениеПользователя("Планировщик инициализирован.",,, БиблиотекаКартинок.Сегодня);
КонецПроцедуры


&НаСервере
Процедура ИнициализироватьПланировщикНаСервере()  
	
	Планировщик.НачалоПериодаОтображения = НачалоДня(Дата);
	Планировщик.КонецПериодаОтображения	= КонецДня(Дата);
	
	ПериодОтображенияНачало	= Планировщик.НачалоПериодаОтображения + (Константы.ЧасНачалаРабочегоДня.Получить()	- НачалоДня(Константы.ЧасНачалаРабочегоДня.Получить()));
	ПериодОтображенияКонец = Планировщик.НачалоПериодаОтображения + (Константы.ЧасОкончанияРабочегоДня.Получить() - НачалоДня(Константы.ЧасОкончанияРабочегоДня.Получить()));
	
	Планировщик.ТекущиеПериодыОтображения.Очистить();
	Планировщик.ТекущиеПериодыОтображения.Добавить(ПериодОтображенияНачало, ПериодОтображенияКонец);
	
	ПланировщикЗаполнитьИзмерения();
	ПланировщикЗаполнитьРасписание();
	
КонецПроцедуры


&НаСервере
Процедура ПланировщикЗаполнитьИзмерения() 
	
	// Инициализируем измерения планировщика
	ИзмеренияПланировщика = Планировщик.Измерения;
	ИзмеренияПланировщика.Очистить();
	 
	ЗапросИзмерения = Новый Запрос(
	"ВЫБРАТЬ
	|	Помещения.Ссылка КАК Помещение
	|ИЗ
	|	Справочник.Помещения КАК Помещения
	|ГДЕ
	|	НЕ Помещения.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Тренеры.Ссылка КАК Тренер
	|ИЗ
	|	Справочник.Тренеры КАК Тренеры
	|ГДЕ
	|	НЕ Тренеры.ПометкаУдаления"
	);
	
	// Поиск помещений и тренеров
	РезультатИзмеренияВыполнить = ЗапросИзмерения.ВыполнитьПакет();
	ВыборкаПомещений = РезультатИзмеренияВыполнить[0].Выбрать();
	ВыборкаТренеров = РезультатИзмеренияВыполнить[1].Выбрать();
	
	Отказ = Ложь;
	Если Не ВыборкаПомещений.Количество() Тогда
		глСообщениеПользователю("Не заполнен справочник помещения.", УникальныйИдентификатор);
		Отказ = Истина;
	КонецЕсли;
	Если Не ВыборкаТренеров.Количество() Тогда
		глСообщениеПользователю("Не заполнен справочник тренеры.", УникальныйИдентификатор);
		Отказ = Истина;
	КонецЕсли;
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Создание группы Помещения
	ИзмерениеПомещения = ИзмеренияПланировщика.Добавить("Помещения");
	Пока ВыборкаПомещений.Следующий() Цикл
		
		// Добавление помещений в группу Помещения
		НовоеПомещение = ИзмерениеПомещения.Элементы.Добавить(ВыборкаПомещений.Помещение);
		НовоеПомещение.Текст = ВыборкаПомещений.Помещение.Наименование;
		
	КонецЦикла;
	
	// Создание измерения Тренеры
	ИзмерениеТренеры = ИзмеренияПланировщика.Добавить("Тренеры");
	Пока ВыборкаТренеров.Следующий() Цикл
		
		// Заполнение измерений тренеры
		НовыйТренер = ИзмерениеТренеры.Элементы.Добавить(ВыборкаТренеров.Тренер);
		НовыйТренер.Текст = ВыборкаТренеров.Тренер.Наименование;
		
	КонецЦикла;
	
КонецПроцедуры


&НаСервере
Процедура ПланировщикЗаполнитьРасписание()  
	
	// Инициализация элементов планировщика (записи по измерениям)
	ЭлементыПланировщика = Планировщик.Элементы;
	ЭлементыПланировщика.Очистить();
	
	ЗапросПланы = Новый Запрос(
	"ВЫБРАТЬ
	|	РасписаниеТренеров.Помещение,
	|	РасписаниеТренеров.Тренер,
	|	РасписаниеТренеров.ВремяС,
	|	РасписаниеТренеров.ВремяПо,
	|	РасписаниеТренеров.ГруппаСпортсменов
	|ИЗ
	|	РегистрСведений.РасписаниеТренеров КАК РасписаниеТренеров
	|ГДЕ
	|	РасписаниеТренеров.Период МЕЖДУ &НачалоПериода И &КонецПериода"
	);
	ЗапросПланы.УстановитьПараметр("НачалоПериода",	НачалоДня(Дата));
	ЗапросПланы.УстановитьПараметр("КонецПериода",	КонецДня(Дата));
	
	РезультатПланыВыполнить = ЗапросПланы.Выполнить();
	Если РезультатПланыВыполнить.Пустой() Тогда
		глСообщениеПользователю("Нет планов на выбранную дату.", УникальныйИдентификатор);
		Возврат;
	КонецЕсли;
	
	НачалоДаты = НачалоДня(Дата);
	
	РезультатПланы = РезультатПланыВыполнить.Выбрать();
	Пока РезультатПланы.Следующий() Цикл
		
		// Связка записей с измерениями выполняется через соответствия
		СоответствиеЗначений = Новый Соответствие;
		СоответствиеЗначений.Вставить("Помещения", РезультатПланы.Помещение);
		СоответствиеЗначений.Вставить("Тренеры", РезультатПланы.Тренер);
		
		РазницаВремениНачало = РезультатПланы.ВремяС - НачалоДня(РезультатПланы.ВремяС);
		РазницаВремениКонец	= РезультатПланы.ВремяПо - НачалоДня(РезультатПланы.ВремяПо);
		
		Начало = НачалоДаты + РазницаВремениНачало;
		Конец = НачалоДаты + РазницаВремениКонец;
		
		// Добавление новой записи в промежуток времени
		НовыйЭлемент = ЭлементыПланировщика.Добавить(Начало, Конец);
		НовыйЭлемент.ЗначенияИзмерений = Новый ФиксированноеСоответствие(СоответствиеЗначений);
		НовыйЭлемент.Текст = РезультатПланы.ГруппаСпортсменов;
		
	КонецЦикла;
	
КонецПроцедуры // ПланировщикЗаполнитьРасписание


# КонецОбласти
