&НаСервереБезКонтекста
Функция ПолучитьКартуПоСпортсмену(СсылкаНаСпортсмена)
    Если СсылкаНаСпортсмена = Неопределено Тогда
        Возврат Неопределено;
    КонецЕсли;

    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ ПЕРВЫЕ 1
         |    КлубныеКарты.Ссылка КАК Карта,
         |    КлубныеКарты.Наименование КАК Название
         |ИЗ
         |    Справочник.КлубныеКарты КАК КлубныеКарты
         |ГДЕ
         |    КлубныеКарты.ВладелецКарты = &Спортсмен
         |    И НЕ КлубныеКарты.ПометкаУдаления";

    Запрос.УстановитьПараметр("Спортсмен", СсылкаНаСпортсмена);

    Выборка = Запрос.Выполнить().Выбрать();
    Если Выборка.Следующий() Тогда
        Возврат Выборка.Карта;
    Иначе
        Возврат Неопределено;
    КонецЕсли;
КонецФункции


&НаКлиенте
Процедура СпортсменПриИзменении(Элемент)
    Объект.КлубнаяКарта = ПолучитьКартуПоСпортсмену(Объект.Спортсмен);
КонецПроцедуры


// Работа с ценами
&НаСервереБезКонтекста
Функция ПолучитьЦеныУслуг(МассивУслуг)

    Если МассивУслуг.Количество() = 0 Тогда
        Возврат Новый Соответствие;
    КонецЕсли;

    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ
         |    ЦеныУслуг.Услуга КАК Услуга,
         |    МАКСИМУМ(ЦеныУслуг.Цена) КАК Цена
         |ИЗ
         |    РегистрСведений.ЦеныУслуг КАК ЦеныУслуг
         |ГДЕ
         |    ЦеныУслуг.Услуга В (&Услуги)
         |    И ЦеныУслуг.Период <= &Период
         |СГРУППИРОВАТЬ ПО
         |    ЦеныУслуг.Услуга";

    Запрос.УстановитьПараметр("Услуги", МассивУслуг);
    Запрос.УстановитьПараметр("Период", ТекущаяДата());

    Результат = Запрос.Выполнить().Выгрузить();

    Цены = Новый Соответствие;
    Для Каждого Стр Из Результат Цикл
        Цены.Вставить(Стр.Услуга, Стр.Цена);
    КонецЦикла;

    Возврат Цены;
КонецФункции

&НаКлиенте
Процедура ПересчитатьСтрокуАбонемента(Строка, Цены)
    Если Строка = Неопределено Тогда
        Возврат;
    КонецЕсли;

    Количество = ?(Строка.КоличествоПосещений = Неопределено, 0, Строка.КоличествоПосещений);
    ЦенаОдного = Цены.Получить(Строка.Услуга); 
    Если ЦенаОдного = Неопределено Тогда
        ЦенаОдного = 0;
    КонецЕсли;

    Строка.Сумма = Количество * ЦенаОдного;

КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьВсеАбонементы()

    Услуги = Новый Массив;
    Для Каждого Строка Из Объект.Абонементы Цикл
        Если ЗначениеЗаполнено(Строка.Услуга) Тогда
            Услуги.Добавить(Строка.Услуга);
        КонецЕсли;
    КонецЦикла;

    Цены = ПолучитьЦеныУслуг(Услуги);

    Для Каждого Строка Из Объект.Абонементы Цикл
        ПересчитатьСтрокуАбонемента(Строка, Цены);
    КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура АбонементыУслугаПриИзменении(Элемент)
    ПересчитатьВсеАбонементы();
КонецПроцедуры

&НаКлиенте
Процедура АбонементыКоличествоПосещенийПриИзменении(Элемент)
    ПересчитатьВсеАбонементы();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    ПересчитатьВсеАбонементы();
КонецПроцедуры






&НаКлиенте
Процедура Оплатить(Команда)
    Если Не Объект.Проведен Тогда
        ПоказатьПредупреждение(, "Документ должен быть проведен перед оплатой");
        Возврат;
    КонецЕсли;
    ОткрытьФормуОплаты();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуОплаты()
    ПараметрыФормы = Новый Структура; 
    ПараметрыФормы.Вставить("Основание", Объект.Ссылка); //Ссылка на текущий документ продажи
    ОткрытьФорму("Документ.Оплата.Форма.ФормаДокумента", ПараметрыФормы);    
КонецПроцедуры


